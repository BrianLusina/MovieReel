buildscript {
    ext.kotlin_version = '1.2.41'
    ext.dokka_version = "0.9.16"
    ext.detektVersion = "1.0.0.RC6-4"

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven {url "https://maven.fabric.io/public"}
    }

    dependencies {
        classpath "io.fabric.tools:gradle:1+"
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath 'com.github.triplet.gradle:play-publisher:1.2.0'
        classpath "com.dicedmelon.gradle:jacoco-android:0.1.2"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${dokka_version}"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6-4"
}

apply from: "buildSystem/ci.gradle"
apply from: "dependencies.gradle"

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://maven.fabric.io/public" }
        maven { url "https://jitpack.io" }
        maven { url "http://dl.bintray.com/typesafe/maven-releases" }
        maven { url "http://dl.bintray.com/jetbrains/spek" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task runDomainUnitTests(dependsOn: [':domain:test']) {
    description 'Run unit tests for the domain layer.'
}

task runDataUnitTests(dependsOn: [':data:cleanTestDebugUnitTest', ':data:testDebugUnitTest']) {
    description 'Run unit tests for the data layer.'
}

task runUnitTests(dependsOn: ['runDomainUnitTests', 'runDataUnitTests']) {
    description 'Run unit tests for both domain and data layers.'
}

task runAcceptanceTests(dependsOn: [':presentation:connectedAndroidTest']) {
    description 'Run application acceptance tests.'
}


detekt {
    version = rootProject.ext.detektVersion
    defaultProfile {
        input = file("src/main/kotlin")
        filters = ".*/resources/.*,.*/build/.*"
    }
}